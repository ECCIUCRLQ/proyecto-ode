/*
  sample-pci-device.dml - sample PCI DML code

  This Software is part of Simics. The rights to copy, distribute,
  modify, or otherwise make use of this Software may be licensed only
  pursuant to the terms of an applicable license agreement.
  
  Copyright 2010-2022 Intel Corporation

*/
/*
  Tags for extracting code into Simics manuals:
  <add id="sample-pci-device.dml">
  <insert-until text="// end of sample-pci-device.dml"/>
  </add>
*/

dml 1.4;

device sample_pci_device;
param desc = "sample PCI device";
param documentation = "This is a very simple PCI device.";

import "pci/common.dml";
is pci_device;

param pci_hotplug = true;

attribute int_attr is uint64_attr "An integer attribute";

bank pci_config {

    /* This attribute should contain a list of all BAR registers */
    param base_address_registers = ["base_address_0"];

    register vendor_id { param init_val = 0x104C; } // Texas Instruments
    register device_id { param init_val = 0xAC10; } // PC Card Controller

    register base_address_0 @ 0x10 is (memory_base_address_32) {
        param size_bits = 8;
        param map_func = 1;
    }
    register base_address_1 @ 0x14 is (no_base_address_32);
    register base_address_2 @ 0x18 is (no_base_address_32);
    register base_address_3 @ 0x1C is (no_base_address_32);
    register base_address_4 @ 0x20 is (no_base_address_32);
    register base_address_5 @ 0x24 is (no_base_address_32);
}

bank reg is function_mapped_bank {
    /* Each register bank need to have a unique function number,
       which should match with the map_func param in one BAR */
    param function = 1;
    param register_size = 4;
    register version      @ 0x0010 "Major and minor version" {
        is read_only;
        param init_val = 0x4711;
    }
}

// end of sample-pci-device.dml
